
to make-map
  let xmax max-pxcor - 1
  let xmin 0 - xmax
  let ymax max-pycor - 1
  let ymin 0 - ymax
  
  ask patches 
  [ 
    set lock false 
    set road? false
  ]
  
  ;; Roads and Areas

  make-road-square xmin ymin xmax - xmin ymax - ymin
  
  let mlx round ((xmax - xmin - blocksize) / 2) ; middle left x
  let mrx round ((xmax - xmin + blocksize) / 2) ; middle right x
  let mdy round ((ymax - ymin - blocksize) / 2) ; middle down y
  let muy round ((ymax - ymin + blocksize) / 2) ; middle up y
  make-road-square xmin ymin mlx ymax - ymin
  make-road-square xmin ymin xmax - xmin mdy
  make-road-square xmin ymin mrx ymax - ymin
  make-road-square xmin ymin xmax - xmin muy
  
  let qx round ((xmax - xmin) / 4) ; quarter x
  let qy round ((ymax - ymin) / 4) ; quarter y
  make-road-square xmin ymin qx qy
  make-road-square xmax - qx ymin qx qy
  make-road-square xmax - qx ymax - qy qx qy
  make-road-square xmin ymax - qy qx qy
  
  make-road-square xmin ymin qx ymax - ymin
  make-road-square xmax - qx ymin qx ymax - ymin
  
  make-area-square xmin ymin qx qy "home" "low"
  make-area-square xmax - qx ymin qx qy "home" "low"
  make-area-square xmax - qx ymax - qy qx qy "home" "low"
  make-area-square xmin ymax - qy qx qy "home" "low"
  
  let qx' mlx - qx - 1
  let qy' round (qy / 2)
  make-area-square xmin + qx ymin qx' qy' "industry" "low"
  make-area-square xmin + mrx ymin qx' qy' "industry" "low"
  make-area-square xmin + mrx ymax - qy' qx' qy' "industry" "low"
  make-area-square xmin + qx ymax - qy' qx' qy' "industry" "low"
  
  make-area-square xmin + qx ymin + qy' qx' qy' "shop" "low"
  make-area-square xmin + mrx ymin + qy' qx' qy' "shop" "low"
  make-area-square xmin + mrx ymax - 2 * qy' qx' qy' "shop" "low"
  make-area-square xmin + qx ymax - 2 * qy' qx' qy' "shop" "low"
  
  set qy' mdy - qy
  make-area-square xmin + qx ymin + qy qx' qy' "home" "average"
  make-area-square xmin + mrx ymin + qy qx' qy' "home" "average"
  make-area-square xmin + mrx ymax - qy - qy' qx' qy' "home" "average"
  make-area-square xmin + qx ymax - qy - qy' qx' qy' "home" "average"
  
  set qx' mrx - mlx
  set qy' 2.5 * blocksize
  make-area-square xmin + mlx ymin qx' qy' "industry" "high"
  make-area-square xmin + mlx ymin + qy' qx' mdy - qy' "shop" "average"
  make-area-square xmin + mlx ymax - qy' qx' qy' "industry" "high"
  make-area-square xmin + mlx ymin + muy qx' mdy - qy' "shop" "average"
  
  set qx' qx - 1
  set qy' muy - mdy
  make-area-square xmin ymin + mdy qx' qy' "home" "high"
  make-area-square xmax - qx' ymin + mdy qx' qy' "home" "high"
  set qx' mlx - qx
  make-area-square xmin + qx ymin + mdy qx' qy' "shop" "high"
  make-area-square xmin + mrx ymin + mdy qx' qy' "shop" "high"
  
  make-area-square -1.5 * blocksize + 1 ymin + mdy 3 * blocksize - 2 qy' "service" false
  
  set qx' round(qx / 3)
  set qy' mdy - qy - 1
  make-area-square xmin + qx' ymin + qy qx - qx' qy' "home" "high"
  make-area-square xmin + qx' ymin + muy qx - qx' qy' "home" "high"
  make-area-square xmin ymin + qy qx' qy' "industry" "average"
  make-area-square xmin ymin + muy qx' qy' "industry" "average"
  
  make-area-square xmax - qx ymin + qy qx - qx' qy' "home" "high"
  make-area-square xmax - qx ymin + muy qx - qx' qy' "home" "high"
  make-area-square xmax - qx' ymin + qy qx' qy' "industry" "average"
  make-area-square xmax - qx' ymin + muy qx' qy' "industry" "average"
  
  
  ;; Buildings
  
  create-city_halls 1 [
    set color 9.9
    set size 0.75 * blocksize
    set xcor 0
    set ycor 0
    lock-patches xcor ycor size
  ]
  create-police_stations 1 [
    set color 93
    set size 0.5 * blocksize
    set xcor -1 * blocksize
    set ycor 0
    set worker-quantity 0
    set capacity 4
    set salary 10
    lock-patches xcor ycor size + 1
  ]
  create-fire_departments 1 [
    set color 14
    set size 0.5 * blocksize
    set xcor blocksize
    set ycor 0
    set worker-quantity 0
    set capacity 4
    set salary 10
    lock-patches xcor ycor size + 1
  ]
  
  make-house blocksize blocksize
  make-house blocksize -1 * blocksize
  make-house -1 * blocksize -1 * blocksize
  make-house -1 * blocksize blocksize
  
  make-house 4 * blocksize - 1 0
  make-house -4 * blocksize + 1 0
  
  make-house 4 * blocksize + 1 3 * blocksize + 1
  make-house 4 * blocksize + 1 -3 * blocksize - 1
  make-house -4 * blocksize - 1 -3 * blocksize - 1
  make-house -4 * blocksize - 1 3 * blocksize + 1
  
  make-factory 0 4 * blocksize + 3
  make-factory 0 -4 * blocksize - 3
  
  make-store 3 * blocksize - 1 0
  make-store -3 * blocksize + 1 0
end


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;INTERFACE;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


to make-road-square [ x y w h ]
  let xi x
  let yi y
  while [ xi < x + w]
  [
    make-road xi yi
    set xi xi + 1
  ]
  while [ yi < y + h ]
  [
    make-road xi yi
    set yi yi + 1
  ]
  while [ xi > x ]
  [
    make-road xi yi
    set xi xi - 1
  ]
  while [ yi > y ]
  [
    make-road xi yi
    set yi yi - 1
  ]
end

to make-area-square [ x y w h a d ]
  let xi x
  let yi y
  while [ xi <= x + w ]
  [
    while [ yi <= y + h ]
    [
      ask patch xi yi 
      [
        if(pcolor != 5)
        [
          set area a
          set density d
          set pcolor patch-color a d
          set cost patch-cost a d
        ]
      ]
      set yi yi + 1
    ]
    set yi y
    set xi xi + 1
  ]
end

to lock-patches [ x y s ]
  ask patches with [ pxcor >= x - s / 2 and pxcor <= x + s / 2 and pycor >= y - s / 2 and pycor <= y + s / 2 ] [ set lock true ]
end

to-report patch-color [ a d ]
  if (a = "home")
  [
    if (d = "low") [ report 55 ]
    if (d = "average") [ report 53 ]
    if (d = "high") [ report 51 ]
  ]
  if (a = "industry")
  [
    if (d = "low") [ report 35 ]
    if (d = "average") [ report 33 ]
    if (d = "high") [ report 31 ]
  ]
  if (a = "shop")
  [
    if (d = "low") [ report 105 ]
    if (d = "average") [ report 103 ]
    if (d = "high") [ report 101 ]
  ]
  if (a = "service") [ report 11 ]
  report 0
end

to-report patch-cost [ a d ]
  if (a = "home")
  [
    if (d = "low") [ report 50 ]
    if (d = "average") [ report 30 ]
    if (d = "high") [ report 10 ]
  ]
  if (a = "industry")
  [
    if (d = "low") [ report 500 ]
    if (d = "average") [ report 300 ]
    if (d = "high") [ report 100 ]
  ]
  if (a = "shop")
  [
    if (d = "low") [ report 500 ]
    if (d = "average") [ report 300 ]
    if (d = "high") [ report 100 ]
  ]
  if (a = "service") [ report 100 ]
  report 1000
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;MAKES;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

to make-road [x y]
  ask patch x y 
  [ 
    lock-patches x y round(blocksize / 2 + 1)
    set pcolor 5 
  ]
  ask patch x (y + 1) [ set road? true ]
end

to make-house [x y]
  create-houses 1 [
    set color 10 * random 13 + 9
    set size 0.75 * blocksize
    set xcor x
    set ycor y
    set burning false
    if(density = "low") 
    [ 
      set capacity 2
      let r random 2
      ifelse (r = 0) [ set shape "house ranch" ] [ set shape "house two story" ]
      lock-patches x y 2 * size
    ]
    if(density = "average") 
    [ 
      set capacity 4
      let r random 2
      ifelse (r = 0) [ set shape "house bungalow" ] [ set shape "house efficiency" ]
      lock-patches x y 1.5 * size
    ]
    if(density = "high") 
    [ 
      set capacity 6
      set shape "house colonial"
      lock-patches x y size + 1
    ]
  ]
end

to make-factory [x y]
  create-factories 1 [
    set color 9
    set size 0.75 * blocksize
    set xcor x
    set ycor y
    set burning false
    set worker-quantity 0
    if(density = "low") 
    [ 
      set capacity 3
      set salary 20
      lock-patches x y 2 * size
    ]
    if(density = "average") 
    [ 
      set capacity 6
      set salary 15
      lock-patches x y 1.5 * size
    ]
    if(density = "high") 
    [ 
      set capacity 10
      set salary 10
      lock-patches x y size + 1
    ]
  ]
end

to make-store [x y]
  create-stores 1 [
    set color 9
    set size 0.5 * blocksize
    set xcor x
    set ycor y
    set burning false
    set worker-quantity 0
    if(density = "low") 
    [ 
      set capacity 1
      set salary 20
      lock-patches x y 3 * size
    ]
    if(density = "average") 
    [ 
      set capacity 2
      set salary 15
      lock-patches x y 2.25 * size
    ]
    if(density = "high") 
    [ 
      set capacity 4
      set salary 10
      lock-patches x y 1.5 * size + 1
    ]
  ]
end

to make-human [c x y]
  create-humans 1 [
    set color c
    set size 0.25 * blocksize
    set xcor x
    set ycor y
    set speed 1
    set work nobody
    set residence nobody
    set money 100 + random 40
    set killer false
    
    let r random 100
    ifelse (r < 20)
    [
      set prefered_density "low"
    ]
    [
      ifelse (r > 70)
      [
        set prefered_density "average"
      ]
      [
        set prefered_density "high"
      ]
    ]
  ]
end

to make-fire [x y]
  create-fires 1 [
    set color 14
    set size 0.5 * blocksize
    set xcor x
    set ycor y
    ask turtles in-radius size with [ member? self houses or member? self factories or member? self stores ] [ set burning true ]
  ]
end