
;; LumCity core
;; V. 1.0.0 - Nov. 2014
;; Small game based on SimCity, written in NetLogo
;; from Warlogo by Lois Vanhee, Fabien Hervouet and Jacques Ferber
;; authors : Silvere Gauthier, Lameira Yannick
;;
;; includes the BDI architecture from I. Sakellariou and the Warlogo communications

;; includes some libraries
__includes[ "lumcity-agents.nls" "warlogo-communications.nls" "bdi.nls" "lumcity-map.nls"]

breed [city_halls city_hall]
breed [police_stations police_station]
breed [fire_departments fire_department]
breed [houses house]
breed [factories factory]
breed [stores store]
breed [humans human]

;; Items are declared as objects (e.g. turtles out of the main loop). So they can more easily be
;; moved and their status are easier to maintain. It could have been handled with patches. 
breed [fires fire]

;; A correct hierarchy should have avoided such redundancy
city_halls-own [ beliefs incoming-queue intentions ]
police_stations-own [ beliefs capacity incoming-queue intentions salary worker-quantity ]
fire_departments-own [ beliefs capacity incoming-queue intentions salary worker-quantity ]
houses-own [ beliefs burning capacity incoming-queue intentions people-quantity ]
factories-own [ beliefs burning capacity incoming-queue intentions salary worker-quantity ]
stores-own [ beliefs burning capacity incoming-queue intentions salary worker-quantity ]
humans-own [ beliefs blocked incoming-queue intentions killer money percepts prefered_density residence speed work ]

patches-own [ area cost density lock road? ]

globals [ blocksize builds burns funds min_taxe nb-humans ]


to setup
  clear-all
  
  ;; globals parameters
  set blocksize round ((world-width - 8) / 13) ;; valeurs conseill√©es pour les dimensions : 69 et 49 ou multiples entiers
  set builds []
  set burns []
  set funds 100
  set min_taxe 10
  set nb-humans 36
  
  ;; standard initialization
  set-default-shape city_halls "building institution"
  set-default-shape police_stations "police"
  set-default-shape fire_departments "fire department"
  set-default-shape houses "house"
  set-default-shape factories "factory"
  set-default-shape stores "building store"
  set-default-shape humans "person"
  set-default-shape fires "fire"
  
  make-map
  
  ;; make humans
  let i 0
  while [i < nb-humans]
  [
    set i i + 1
    make-human 28 0 -0.25 * blocksize
  ]
  
  ;; assign residences
  ask humans with [ residence = nobody ]
  [ 
    own-residence one-of houses with [ people-quantity < capacity ]
  ]
  
  ask police_stations with [ worker-quantity < capacity ]
  [
    set i worker-quantity
    while [ i < capacity ]
    [
      recruit
      set i i + 1
    ]
  ]
  
  ask fire_departments with [ worker-quantity < capacity ]
  [
    set i worker-quantity
    while [ i < capacity ]
    [
      recruit
      set i i + 1
    ]
  ]
  
  ask stores with [ worker-quantity < capacity ]
  [
    set i worker-quantity
    while [ i < capacity ]
    [
      recruit
      set i i + 1
    ]
  ]
  
  ask factories with [ worker-quantity < capacity ]
  [
    set i worker-quantity
    while [ i < capacity ]
    [
      recruit
      set i i + 1
    ]
  ]
  
  ;; setting initial heading for moving agents
  ask humans [set-random-heading]
  
  reset-ticks
end


;; MAIN LOOP
to go
  if (count humans = 0 or funds <= 0) [
    ask city_halls [ set label "GAME OVER" ]
    print (list "humans : " count(humans) ", funds : " (max list round(funds) 0))
    beep
    stop 
  ]
  tick
  
  ask city_halls [ run city_hall-action ]
  ask police_stations [ set capacity Services-quantity run police_station-action ]
  ask fire_departments [ set capacity Services-quantity run fire_department-action ]
  ask houses with [not burning] [ run house-action ]
  ask factories with [not burning] [ run factory-action ]
  ask stores with [not burning] [ run store-action ]
  ask humans [ run human-action ]
  
  let x 0
  let y 0
  if( builds != [] )
  [
    let name first builds
    set builds but-first builds
    set x first builds
    set builds but-first builds
    set y first builds
    set builds but-first builds
    
    if( not [lock] of patch x y )
    [
      if( name = "house" )
      [
        make-house x y
      ]
      if( name = "industry" )
      [
        make-factory x y
      ]
      if( name = "shop" )
      [
        make-store x y
      ]
    ]
  ]
  
  if( burns != [] )
  [
    let t first burns
    if( t + 500 <= ticks )
    [
      set burns but-first burns
      set x first burns
      set burns but-first burns
      set y first burns
      set burns but-first burns
      
      erase x y
    ]
  ]
  
  if(ticks mod 20 = 0)
  [
    ask humans [ earn-money ]
    
    increase-funds
    decrease-funds
    plot funds
  ]
  
  if(ticks mod 50 = 0)
  [ 
    make-human 28 0 -0.25 * blocksize
  ]
  
  if(random 1000 < accidents-rate) [ burn one-of turtles with [ member? self houses or member? self factories or member? self stores ] ]
  if(random 1000 < accidents-rate) [ ask one-of humans with [ work != nobody and not member? work police_stations ] [ set shape "person killer" set killer true ] ]
  
  ifelse(debug) [ 
    ask humans [ set label round(money) ] 
    ask patches with [ pcolor != 0 and pcolor != 5 and lock ] [ set pcolor 0 ]
    
    let mhc 0 ;max_house_capacity
    let mwc 0 ;max_worker_capacity
    ask turtles with [ not (member? self humans or member? self city_halls or member? self fires) ]
    [
      ifelse(member? self houses) [ set label (list people-quantity " : " capacity) set mhc mhc + capacity ] [ set label (list worker-quantity " : " capacity) set mwc mwc + capacity ]
    ]
    show (list "max_house_capacity = " mhc ", max_worker_capacity = " mwc ", worker-capacity" (sum [capacity - worker-quantity] of factories + sum [capacity - worker-quantity] of stores) ", no-work-humans" count humans with [ work = nobody ])
  ]
  [
    ask turtles [ set label "" ]
    ask patches [ set pcolor patch-color area density ]
    ask patches with [ road? ] [ ask patch pxcor (pycor - 1) [ set pcolor 5 ] ]
  ]
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;TURTLES ACTIONS;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

to move
  let last-move-lenght 0
  while [last-move-lenght < speed] [
    fd 1
    set last-move-lenght last-move-lenght + 1
    if any? other turtles-here with [is-colliding? myself] or hitting-north-wall? self or hitting-south-wall? self or hitting-east-wall? self or hitting-west-wall? self
    [ set blocked true ]
  ]
end


to idle
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;OBSERVER ACTIONS;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

to erase [ x y ]
  ask turtles with [ xcor = x and ycor = y ]
  [
    let s self
    if(member? self houses)
    [
      ask humans with [ residence = s ] [ set residence nobody ]
      die
    ]
    if(member? self factories or member? self stores)
    [
      ask humans with [ work = s ] [ set work nobody set shape "person" ]
      die
    ]
    if(member? self fires) [ die ]
  ]
end

to burn [ t ]
  if(t != nobody)
  [
    make-fire [xcor] of t [ycor] of t
    set burns lput ticks burns
    set burns lput [xcor] of t burns
    set burns lput [ycor] of t burns
  ]
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;BUILDINGS ACTIONS;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

to recruit
  let s "person"
  ifelse(member? self police_stations) [ set s "person police" ]
  [ ifelse(member? self fire_departments) [ set s "person fire" ]
    [ ifelse(member? self stores) [ set s "person service" ]
      [ if(member? self factories) [ set s "person construction" ]]]]
  let d density
  let h one-of humans with [ work = nobody and (prefered_density = d or density = false) ]
  if(worker-quantity < capacity and h != nobody)
  [
    set worker-quantity worker-quantity + 1
    let w self
    ask h [ set work w set shape s]
  ]
end

to kick
  let s self
  let h one-of humans with [ work = s ]
  if(h != nobody)
  [
    ask h [ set shape "person" set work nobody ]
    set worker-quantity worker-quantity - 1
  ]
end

to pay [ h ]
  let w self
  let s salary + random min_taxe
  ask h [ set money money + s ]
end

;; reserved to city_hall
to install-work
  let p one-of patches with [ lock = false and (area = "industry" or area = "shop") ]
  if(p != nobody and ticks mod 50 = 0)
  [
    set builds lput [area] of p builds
    set builds lput [pxcor] of p builds
    set builds lput [pycor] of p builds
    set funds funds - [cost] of p
  ]
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;HUMANS ACTIONS;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

to quit
  if(work != nobody)
  [
    ask work [ set worker-quantity worker-quantity - 1 ]
  ]
  if(residence != nobody)
  [
    ask residence [ set people-quantity people-quantity - 1 ]
  ]
  die
end

to stop-fire
  let fireman (member? work fire_departments)
  if(fireman)
  [
    let s self
    ask fires with [ is-colliding? s ]
    [
      let px position xcor burns
      let py position ycor burns
      if(px != false and py != false and py = px + 1)
      [
        set burns remove-item py burns
        set burns remove-item px burns
        set burns remove-item (px - 1) burns
      ]
      die 
    ]
  ]
end

to arrest-killer
  let policeman (work != nobody and member? work police_stations)
  if(policeman)
  [
    let s self
    ask humans with [ killer = true and is-colliding? s] [ quit ]
  ]
end

to kill
  if(killer = true)
  [
    let s self
    let h one-of humans with [ is-colliding? s and self != s]
    if(h != nobody) [ ask h [ quit ] ]
  ]
end

to install-house
  let s self
  let h one-of houses with [ is-colliding? s and people-quantity < capacity ]
  ifelse(h = nobody)
  [ 
    if(not lock and money > cost)
    [
      set builds lput "house" builds
      set builds lput xcor builds
      set builds lput ycor builds
      set money money - cost
    ]
  ]
  [
    own-residence h
  ]
end

to own-residence [ h ]
  set residence h
  if(h != nobody)
  [
    ask h [ set people-quantity people-quantity + 1 ]
  ]
end

to earn-money
  if(work != nobody)
  [
    let h self
    ask work [ pay h ]
  ]
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;COMMUNICATION;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


to send-message [receiver performative content]
  let msg create-message performative content
  send receiver msg
end


to reply [input-msg performative content]
  if show-messages [ show (sentence "Reply: " performative content " to: " (get-sender input-msg)) ]
  let msg create-message performative content
  send (get-sender input-msg) msg
end

to broadcast-message [lst performative content]
  let msg create-message performative content
  broadcast lst msg
end



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;TESTING OPERATIONS;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


to-report is-building? [a]
  report is-city_hall? a or is-police_station? a or is-fire_department? a or is-house? a or is-factory? a or is-store? a
end

to-report in-front? [a]
  report member? a ([percepts] of myself) in-cone 2 10
end

to-report hitting-north-wall? [a]
  report [pycor] of a = max-pycor
end

to-report hitting-south-wall? [a]
  report [pycor] of a = min-pycor
end

to-report hitting-east-wall? [a]
  report [pxcor] of a = max-pxcor
end

to-report hitting-west-wall? [a]
  report [pxcor] of a = min-pxcor
end



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;SENSING;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


to-report get-heading
  report heading
end


to-report blocked?
  report blocked
end


to-report _perceive
  report other turtles in-radius 2
end

to-report perceive
  report percepts
end


to-report is-colliding? [o]
  report distance o < (size + [size] of o) / 3
end


to-report headed-towards? [o]
  report towards o = heading
end


to set-heading [a]
  if a != nobody [
  ifelse is-turtle? a
  [set heading towards a]
  [set heading a]
  ]
end


to set-random-heading
  set heading random 360
end


to-report in-house?
  report any? houses with [is-colliding? myself]
end


to-report get-closest [l]
  report min-one-of l [distance myself]
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;INTERFACE;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

to increase-funds
  ask humans
  [ 
    let taxe max (list (money * Taxe-per-human / 100) min_taxe)
    set money money - taxe
    set funds funds + taxe
  ]
end

to decrease-funds
  set funds funds - (count turtles - count humans + count patches with [ road? ] / 2 + 2 * Services-quantity) * City-entretien / 100
end
