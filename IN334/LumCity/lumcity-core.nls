
;; LumCity core
;; V. 1.0.0 - Nov. 2014
;; Small game based on SimCity, written in NetLogo
;; from Warlogo by Lois Vanhee, Fabien Hervouet and Jacques Ferber
;; authors : Silvere Gauthier, Lameira Yannick
;;
;; includes the BDI architecture from I. Sakellariou and the Warlogo communications

;; includes some libraries
__includes[ "lumcity-agents.nls" "warlogo-communications.nls" "bdi.nls" "lumcity-map.nls"]

breed [city_halls city_hall]
breed [police_stations police_station]
breed [fire_departments fire_department]
breed [houses house]
breed [factories factory]
breed [stores store]
breed [humans human]

;; Items are declared as objects (e.g. turtles out of the main loop). So they can more easily be
;; moved and their status are easier to maintain. It could have been handled with patches. 
breed [fires fire]
breed [trees tree]

;; A correct hierarchy should have avoided such redundancy
city_halls-own [ beliefs incoming-queue intentions ]
police_stations-own [ beliefs capacity incoming-queue intentions salary worker-quantity ]
fire_departments-own [ beliefs capacity incoming-queue intentions salary worker-quantity ]
houses-own [ beliefs capacity incoming-queue intentions people-quantity ]
factories-own [ beliefs capacity incoming-queue intentions salary worker-quantity ]
stores-own [ beliefs capacity incoming-queue intentions salary worker-quantity ]
humans-own [ beliefs blocked incoming-queue intentions money percepts residence speed work ]

patches-own [ area cost density lock road? ]

globals [ blocksize builds debug founds nb-humans ]


to setup
  clear-all
  
  ;; debug mode
  set debug true
  
  ;; globals parameters
  set nb-humans 72
  set founds 100
  set blocksize round ((world-width - 8) / 13) ;; valeurs conseill√©es pour les dimensions : 69 et 49 ou multiples entiers
  set builds []
  
  ;; standard initialization
  set-default-shape city_halls "building institution"
  set-default-shape police_stations "police"
  set-default-shape fire_departments "fire department"
  set-default-shape houses "house"
  set-default-shape factories "factory"
  set-default-shape stores "building store"
  set-default-shape humans "person"
  
  make-map
  
  ;; make humans
  let i 0
  while [i < nb-humans]
  [
    set i i + 1
    make-human 28 0 -0.25 * blocksize
  ]
  
  ;; assign residences
  ask humans with [ residence = nobody ]
  [ 
    own-residence one-of houses with [ people-quantity < capacity ]
  ]
  
  ask police_stations with [ worker-quantity < capacity ]
  [
    set i worker-quantity
    while [ i < capacity ]
    [
      recruit
      set i i + 1
    ]
  ]
  
  ask fire_departments with [ worker-quantity < capacity ]
  [
    set i worker-quantity
    while [ i < capacity ]
    [
      recruit
      set i i + 1
    ]
  ]
  
  ask stores with [ worker-quantity < capacity ]
  [
    set i worker-quantity
    while [ i < capacity ]
    [
      recruit
      set i i + 1
    ]
  ]
  
  ask factories with [ worker-quantity < capacity ]
  [
    set i worker-quantity
    while [ i < capacity ]
    [
      recruit
      set i i + 1
    ]
  ]
  
  ;; setting initial heading for moving agents
  ask humans [set-random-heading]
  
  if(debug)
  [
    let mhc 0 ;max_house_capacity
    let mwc 0 ;max_worker_capacity
    ask turtles [ 
      if(not (member? self humans or member? self city_halls)) 
      [
        set label capacity 
        ifelse (member? self houses) [ set mhc mhc + capacity ] [ set mwc mwc + capacity ]
      ]
    ]
    show (list "max_house_capacity = " mhc ", max_worker_capacity = " mwc)
    ask patches with [ lock and pcolor != 5 ] [ set pcolor 0 ]
  ]
  reset-ticks
end


;; MAIN LOOP
to go
  if (count humans = 0 or founds <= 0) [
    print "game over !"
    print (list "humans : " count(humans) ", founds : " (max list round(founds) 0))
    beep
    stop 
  ]
  tick
  
  ask city_halls [ run city_hall-action ]
  ask police_stations [ run police_station-action ]
  ask fire_departments [ run fire_department-action ]
  ask houses [ run house-action ]
  ask factories [ run factory-action ]
  ask stores [ run store-action ]
  ask humans [ run human-action ]
  
  if( builds != [] )
  [
    let name first builds
    set builds but-first builds
    let x first builds
    set builds but-first builds
    let y first builds
    set builds but-first builds
    
    if( not [lock] of patch x y )
    [
      if( name = "house" )
      [
        make-house x y
      ]
      if( name = "industry" )
      [
        make-factory x y
      ]
      if( name = "shop" )
      [
        make-store x y
      ]
    ]
  ]
  
  ask humans [ earn-money ]
  
  increase-founds
  decrease-founds
  plot Founds
  
  if(debug) [ 
    ask humans [ set label round(money) ] 
    ask patches with [ pcolor != 0 and pcolor != 5 and lock ] [ set pcolor 0 ]
  ]
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;ACTIONS;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

to move
  let last-move-lenght 0
  while [last-move-lenght < speed] [
    fd 1
    set last-move-lenght last-move-lenght + 1
    if any? other turtles-here with [is-colliding? myself] or hitting-north-wall? self or hitting-south-wall? self or hitting-east-wall? self or hitting-west-wall? self
    [ set blocked true ]
  ]
end


to idle
end

;; reserved to factories, stores or services
to recruit
  let s "person"
  ifelse(member? self police_stations) [ set s "person police" ]
  [ ifelse(member? self fire_departments) [ set s "person fire" ]
    [ ifelse(member? self stores) [ set s "person service" ]
      [ if(member? self factories) [ set s "person construction" ]]]]
  let h one-of humans with [ work = nobody ]
  if(worker-quantity < capacity and h != nobody)
  [
    set worker-quantity worker-quantity + 1
    let w self
    ask h [ set work w set shape s]
  ]
end

;; reserved to factories, stores or services
to pay [ h ]
  let w self
  let s salary
  ask h [ set money money + s ]
end

;; reserved to humans
to quit
  if(work != nobody)
  [
    ask work [ set worker-quantity worker-quantity - 1 ]
    set work nobody
  ]
end

;; reserved to city_hall
to install-work
  let p one-of patches with [ lock = false and (area = "industry" or area = "shop") ]
  if(p != nobody and ticks mod 50 = 0)
  [
    set builds lput [area] of p builds
    set builds lput [pxcor] of p builds
    set builds lput [pycor] of p builds
    set founds founds - [cost] of p
  ]
end

;; reserved to humans
to install-house
  let h one-of houses-here with [ people-quantity < capacity ]
  ifelse(h = nobody)
  [ 
    if(not lock and money > cost)
    [
      set builds lput "house" builds
      set builds lput xcor builds
      set builds lput ycor builds
      set money money - cost
    ]
  ]
  [
    own-residence h
  ]
end

to own-residence [ h ]
  set residence h
  if(h != nobody)
  [
    ask h [ set people-quantity people-quantity + 1 ]
  ]
end

;; reserved to humans
to earn-money
  if(work != nobody and ticks mod 50 = true)
  [
    let h self
    ask work [ pay h ]
  ]
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;COMMUNICATION;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


to send-message [receiver performative content]
  let msg create-message performative content
  send receiver msg
end


to reply [input-msg performative content]
  if show-messages [ show (sentence "Reply: " performative content " to: " (get-sender input-msg)) ]
  let msg create-message performative content
  send (get-sender input-msg) msg
end

to broadcast-message [lst performative content]
  let msg create-message performative content
  broadcast lst msg
end



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;TESTING OPERATIONS;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


to-report is-building? [a]
  report is-city_hall? a or is-police_station? a or is-fire_department? a or is-house? a or is-factory? a or is-store? a
end

to-report in-front? [a]
  report member? a ([percepts] of myself) in-cone 2 10
end

to-report hitting-north-wall? [a]
  report [pycor] of a = max-pycor
end

to-report hitting-south-wall? [a]
  report [pycor] of a = min-pycor
end

to-report hitting-east-wall? [a]
  report [pxcor] of a = max-pxcor
end

to-report hitting-west-wall? [a]
  report [pxcor] of a = min-pxcor
end



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;SENSING;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


to-report get-heading
  report heading
end


to-report blocked?
  report blocked
end


to-report _perceive
  report other turtles in-radius 2
end

to-report perceive
  report percepts
end


to-report is-colliding? [o]
  report distance o < (size + [size] of o) / 3
end


to-report headed-towards? [o]
  report towards o = heading
end


to set-heading [a]
  if a != nobody [
  ifelse is-turtle? a
  [set heading towards a]
  [set heading a]
  ]
end


to set-random-heading
  set heading random 360
end


to-report in-house?
  report any? houses with [is-colliding? myself]
end


to-report get-closest [l]
  report min-one-of l [distance myself]
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;INTERFACE;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

to increase-founds
  ask humans
  [ 
    let taxe money * Taxe-per-human / 100
    set money money - taxe
    set founds founds + taxe
  ]
end

to decrease-founds
  set founds founds - (count turtles - count humans) * City-entretien / 100
end
